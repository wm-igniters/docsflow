pipeline {
  agent any

  tools {
    nodejs 'node-22.11.0'
  }

  environment {
    REMOTE_SERVER = "172.31.27.164"
    AWS_REGION = 'us-east-1'
    ECR_REPO = 'poc-misc'
    DEPLOYMENT_KEY = 'wm-docs-ssh-key'

    AUTH_SECRET = credentials('auth_secret')
    AUTH_GOOGLE_ID = credentials('auth_google_id')
    AUTH_GOOGLE_SECRET = credentials('auth_google_secret')
    MONGODB_URI = credentials('mongodb_uri')
    GITHUB_TOKEN = credentials('docs_flow_github_token')
    GITHUB_WEBHOOK_SECRET = credentials('docs_flow_github_secret')
  }

  stages {

    stage('Set Environment Based on Branch') {
      steps {
        script {

          def rawBranch = (env.BRANCH_NAME ?: env.GIT_BRANCH ?: '')
          def branchName = rawBranch.replaceFirst(/^origin\//, '').trim()
          if (!branchName) {
            error "Unable to resolve branch name. Ensure this runs in a multibranch pipeline or set BRANCH_NAME/GIT_BRANCH."
          }

          def branchConfig = [
            dev: [
              container: "dev-wm-docs-flow",
              port     : "8022",
              authUrl  : "https://next-docsflow.wavemaker.ai",
              imageTag : "dev.wm-docs-flow.${env.BUILD_NUMBER}"
            ],
            main: [
              container: "wm-docs-flow",
              port     : "8035",
              authUrl  : "https://docsflow.wavemaker.ai",
              imageTag : "prod.wm-docs-flow.${env.BUILD_NUMBER}"
            ]
          ]

          def config = branchConfig[branchName]
          if (!config) {
            error "Unsupported branch for deploy: ${branchName}"
          }

          env.CONTAINER_NAME = config.container
          env.HTTP_PORT      = config.port
          env.AUTH_URL       = config.authUrl
          env.IMAGE_TAG      = config.imageTag

          echo "Deploying branch: ${env.BRANCH_NAME}"
          echo "Container: ${env.CONTAINER_NAME}"
          echo "Port: ${env.HTTP_PORT}"
          echo "Auth URL: ${env.AUTH_URL}"
        }
      }
    }

    stage('Build Docker Image') {
      steps {
        script {
          withAWS(region: AWS_REGION, credentials: 'wm-ps-user-ecr') {
            def awsAccountID = sh(
              returnStdout: true,
              script: "aws sts get-caller-identity --query Account --output text"
            ).trim()

            sh """
              aws ecr get-login-password --region ${AWS_REGION} |
              docker login --username AWS --password-stdin \
              ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com

              docker build \
                --build-arg MONGODB_URI=dummy \
                --tag ${ECR_REPO}:${IMAGE_TAG} .
            """
          }
        }
      }
    }

    stage('Push Image to ECR') {
      steps {
        script {
          withAWS(region: AWS_REGION, credentials: 'wm-ps-user-ecr') {
            def awsAccountID = sh(
              returnStdout: true,
              script: "aws sts get-caller-identity --query Account --output text"
            ).trim()

            sh """
              docker tag ${ECR_REPO}:${IMAGE_TAG} \
                ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com/${ECR_REPO}:${IMAGE_TAG}

              docker push \
                ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com/${ECR_REPO}:${IMAGE_TAG}
            """
          }
        }
      }
    }

    stage('Deploy Docker Container') {
      steps {
        script {
          sshagent([DEPLOYMENT_KEY]) {
            withAWS(region: AWS_REGION, credentials: 'wm-ps-user-ecr') {

              def awsAccountID = sh(
                returnStdout: true,
                script: "aws sts get-caller-identity --query Account --output text"
              ).trim()

              def containerExists = sh(
                returnStdout: true,
                script: "ssh -o StrictHostKeyChecking=no ubuntu@${REMOTE_SERVER} 'docker ps -a --filter name=${CONTAINER_NAME} --format \"{{.Names}}\"'"
              ).trim()

              if (containerExists) {
                sh "ssh -o StrictHostKeyChecking=no ubuntu@${REMOTE_SERVER} 'docker rm -f ${CONTAINER_NAME}'"
              }

              sh """
                aws ecr get-login-password --region ${AWS_REGION} |
                ssh -o StrictHostKeyChecking=no ubuntu@${REMOTE_SERVER} \
                'docker login --username AWS --password-stdin \
                ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com'
              """

              sh "ssh -o StrictHostKeyChecking=no ubuntu@${REMOTE_SERVER} 'docker pull ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com/${ECR_REPO}:${IMAGE_TAG}'"

              def shellQuote = { value ->
                def sanitized = (value ?: '').replace("'", "'\"'\"'")
                return "'${sanitized}'"
              }

              def envVars = [
                "AUTH_SECRET=${shellQuote(AUTH_SECRET)}",
                "AUTH_GOOGLE_ID=${shellQuote(AUTH_GOOGLE_ID)}",
                "AUTH_GOOGLE_SECRET=${shellQuote(AUTH_GOOGLE_SECRET)}",
                "MONGODB_URI=${shellQuote(MONGODB_URI)}",
                "GITHUB_TOKEN=${shellQuote(GITHUB_TOKEN)}",
                "GITHUB_WEBHOOK_SECRET=${shellQuote(GITHUB_WEBHOOK_SECRET)}",
                "AUTH_TRUST_HOST='true'",
                "AUTH_URL=${shellQuote(env.AUTH_URL)}"
              ].join(' ')

              def runCmd = """
                ${envVars} docker run -d \
                  --name ${CONTAINER_NAME} \
                  -p ${HTTP_PORT}:3000 \
                  -e AUTH_SECRET \
                  -e AUTH_GOOGLE_ID \
                  -e AUTH_GOOGLE_SECRET \
                  -e MONGODB_URI \
                  -e GITHUB_TOKEN \
                  -e GITHUB_WEBHOOK_SECRET \
                  -e AUTH_TRUST_HOST \
                  -e AUTH_URL \
                  ${awsAccountID}.dkr.ecr.${AWS_REGION}.amazonaws.com/${ECR_REPO}:${IMAGE_TAG}
              """

              sh "ssh -o StrictHostKeyChecking=no ubuntu@${REMOTE_SERVER} ${shellQuote(runCmd)}"
            }
          }
        }
      }
    }
  }
}
